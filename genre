#!/usr/bin/env perl

use Mojolicious::Lite -signatures;
use Data::Dumper::Compact qw(ddc);
use List::Util ();
use Storable qw(retrieve);

use constant ARTIST => 'artists.dat';

get '/' => sub ($c) {
  my $query = $c->param('q') || '';
  my $genre = {};
  my @matches;
  my %all_genres;
  if (-e ARTIST) {
    $genre = retrieve(ARTIST);
  }
  for my $artist (sort keys %$genre) {
    push @matches, $artist
      if List::Util::any { $_ =~ /$query/ } $genre->{$artist}->@*;
    for my $g ($genre->{$artist}->@*) {
      $all_genres{$g}++;
    }
  }
  $c->render(
    template => 'index',
    genre    => ddc($genre),
    genres   => ddc(\%all_genres),
    query    => $query,
    matches  => ddc(\@matches),
  );
} => 'index';

app->start;
__DATA__

@@ index.html.ep
% layout 'default';
% title 'Genres';
<h1>Artist Genres</h1>
<form method="get">
  <input type="text" name="q" placeholder="Search phrase"/>
  <input type="submit" value="Query"/>
</form>
<p></p>
"<b><%= $query %></b>" :
<br>
<pre><%= $matches %></pre>
<p></p>
<pre><%= $genres %></pre>
<hr>
<pre><%= $genre %></pre>

@@ layouts/default.html.ep
<!DOCTYPE html>
<html>
  <head><title><%= title %></title></head>
  <body><%= content %></body>
</html>
