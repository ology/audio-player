#!/usr/bin/env perl

use Mojolicious::Lite -signatures;
use Data::Dumper::Compact qw(ddc);
use List::Util qw(uniq);
use Storable qw(retrieve);

use constant ARTIST => 'artists.dat';
use constant TRACKS => 'audio-player4.dat';

get '/' => sub ($c) {
  my $query = $c->param('q') || '';
  my @artists;
  my %all_genres;
  my $genre = {};
  $genre = retrieve(ARTIST) if -e ARTIST;
  my $audio = {};
  $audio = retrieve(TRACKS) if -e TRACKS;
  for my $artist (sort keys %$genre) {
    for my $g ($genre->{$artist}->@*) {
      push @artists, $artist if $g =~ /$query/;
      $all_genres{$g}++;
    }
  }
  @artists = uniq @artists;
  my @tracks = _query($audio, \@artists);
  $c->render(
    template => 'index',
    genre    => ddc($genre),
    genres   => ddc(\%all_genres),
    query    => $query,
    artists  => ddc(\@artists),
    n        => scalar(@artists),
    tracks   => \@tracks,
    m        => scalar(@tracks),
  );
} => 'index';

sub _query {
  my ($audio, $artists) = @_;
  my @match;
  # Brute force through every track, looking for matches
  for my $key (sort { $a <=> $b } keys %$audio) {
    my $track = $audio->{$key}{track};
    my @parts  = split /\//, $track;
    my $artist = $parts[-3]; # but not always
    push @match, { $key => $track }
      if grep { $artist eq $_ } @$artists;
  }
  return @match;
}

app->start;
__DATA__

@@ index.html.ep
% layout 'default';
% title 'Artist Genres';
<h1><%= title %></h1>
<form method="get">
  <input type="text" name="q" placeholder="Search phrase" value="<%= $query %>"/>
  <input type="submit" value="Query"/>
</form>
<p></p>
<%= $n %> matches:
<br>
<pre><%= $artists %></pre>
<p></p>
<%= $m %> tracks:
<ol>
% for my $track (@$tracks) {
%   my $current = (keys %$track)[0];
%   my $name = (values %$track)[0];
  <li>
    <a href="<%= url_for('index')->query(current => $current, noinc => 1, shuffle => 0, autoplay => 1, autoadvance => 0, darkmode => 1) %>"><%= $name %>
  </li>
% }
</ol>
<!-- Genres:
<pre><%= $genres %></pre>
<hr>
<pre><%= $genre %></pre>
-->

@@ layouts/default.html.ep
<!DOCTYPE html>
<html>
  <head><title><%= title %></title></head>
  <body><%= content %></body>
</html>
