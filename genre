#!/usr/bin/env perl

use Mojolicious::Lite -signatures;
use Data::Dumper::Compact qw(ddc);
use List::Util qw(uniq);
use Storable qw(retrieve);

use constant ARTIST => 'artists.dat';

get '/' => sub ($c) {
  my $query = $c->param('q') || '';
  my @matches;
  my %all_genres;
  my $genre = {};
  $genre = retrieve(ARTIST) if -e ARTIST;
  for my $artist (sort keys %$genre) {
    for my $g ($genre->{$artist}->@*) {
      push @matches, $artist if $g =~ /$query/;
      $all_genres{$g}++;
    }
  }
  @matches = uniq @matches;
  $c->render(
    template => 'index',
    genre    => ddc($genre),
    genres   => ddc(\%all_genres),
    query    => $query,
    matches  => ddc(\@matches),
    n        => scalar(@matches),
  );
} => 'index';

app->start;
__DATA__

@@ index.html.ep
% layout 'default';
% title 'Genres';
<h1>Artist Genres</h1>
<form method="get">
  <input type="text" name="q" placeholder="Search phrase" value="<%= $query %>"/>
  <input type="submit" value="Query"/>
</form>
<p></p>
<%= $n %> matches:
<br>
<pre><%= $matches %></pre>
<p></p>
<pre><%= $genres %></pre>
<hr>
<pre><%= $genre %></pre>

@@ layouts/default.html.ep
<!DOCTYPE html>
<html>
  <head><title><%= title %></title></head>
  <body><%= content %></body>
</html>
